options
{
  STATIC=false;
  LOOKAHEAD=2147483647;
//  DEBUG_PARSER=true;
//  DEBUG_TOKEN_MANAGER=true;
//  DEBUG_LOOKAHEAD=true;
}

PARSER_BEGIN(RdfPathParser)
package at.newmedialab.ldpath.parser;

import at.newmedialab.ldpath.model.Constants;

import at.newmedialab.ldpath.api.backend.*;
import at.newmedialab.ldpath.api.functions.*;
import at.newmedialab.ldpath.api.selectors.*;
import at.newmedialab.ldpath.api.tests.*;
import at.newmedialab.ldpath.api.transformers.*;


import at.newmedialab.ldpath.model.fields.*;
import at.newmedialab.ldpath.model.functions.*;
import at.newmedialab.ldpath.model.programs.*;
import at.newmedialab.ldpath.model.selectors.*;
import at.newmedialab.ldpath.model.tests.*;
import at.newmedialab.ldpath.model.transformers.*;


import java.util.ArrayList;
import java.util.List;
import java.util.Map;
import java.util.HashMap;
import java.util.Locale;
import java.util.Collections;

import java.io.Reader;
import java.io.InputStream;

import java.net.URI;

@SuppressWarnings("all")
public class RdfPathParser<Node> {

        /**
         * A map mapping from namespace prefix to namespace URI
         */
        private Map<String,String> namespaces = new HashMap<String,String>();

        private RDFBackend<Node> backend;


        public RdfPathParser(RDFBackend<Node> backend, Reader in) {
            this(in);
            this.backend = backend;

            initialise();
        }

        public RdfPathParser(RDFBackend<Node> backend, InputStream in) {
            this(in);
            this.backend = backend;

            initialise();
        }

        public RdfPathParser(RDFBackend<Node> backend, InputStream in, String encoding) {
            this(in,encoding);
            this.backend = backend;

            initialise();
        }

        public Program<Node> parseProgram() throws ParseException {
			namespaces.clear();
			namespaces.putAll(Program.DEFAULT_NAMESPACES);

		    return Program();
        }

        public NodeSelector<Node> parseSelector(Map<String,String> ctxNamespaces) throws ParseException {
			namespaces.clear();
			namespaces.putAll(Program.DEFAULT_NAMESPACES);
			if(ctxNamespaces != null) {
			    namespaces.putAll(ctxNamespaces);
			}

		    return Selector();
        }



        public Node resolveResource(String uri) {
            return backend.createURI(uri);
        }


        public String resolveNamespace(String prefix) throws ParseException {
            String uri = namespaces.get(prefix);
            if(uri == null) {
                throw new ParseException("Namespace "+prefix+" not defined!");
            }
            return uri;
        }


        public SelectorFunction<Node> getFunction(String uri) throws ParseException {
            if(xsdNodeFunctionMap.get(uri) != null) {
               return xsdNodeFunctionMap.get(uri);
            } else {
                throw new ParseException("function with URI "+uri+"does not exist");
            }
        }


        public NodeTransformer<?,Node> getTransformer(String uri) throws ParseException {
            if(xsdNodeTransformerMap.get(uri) != null) {
                return xsdNodeTransformerMap.get(uri);
            } else {
                throw new ParseException("transformer with URI "+uri+"does not exist");
            }
        }


        private void initialise() {
            initTransformerMappings();
            initFunctionMappings();
        }

        /**
         * Register the function passed as argument in this parser's function map.
         */
        public void registerFunction(SelectorFunction<Node> function) {
            registerFunction(xsdNodeFunctionMap,function);
        }

        /**
         * Register the result transformer passed as argument for the given type uri.
         */
        public void registerTransformer(String typeUri, NodeTransformer<?,Node> transformer) {
            xsdNodeTransformerMap.put(typeUri,transformer);
        }


        /**
         * A map mapping from XSD types to node transformers.
         */
        private Map<String, NodeTransformer<?,Node>> xsdNodeTransformerMap;
        private void initTransformerMappings() {
            Map<String, NodeTransformer<?,Node>> transformerMap = new HashMap<String, NodeTransformer<?,Node>>();

            transformerMap.put(Constants.NS_XSD + "decimal", new LongTransformer());
            transformerMap.put(Constants.NS_XSD + "integer", new IntTransformer());
            transformerMap.put(Constants.NS_XSD + "long", new LongTransformer());
            transformerMap.put(Constants.NS_XSD + "short", new IntTransformer());
            transformerMap.put(Constants.NS_XSD + "double", new DoubleTransformer());
            transformerMap.put(Constants.NS_XSD + "float", new FloatTransformer());
            transformerMap.put(Constants.NS_XSD + "dateTime", new DateTimeTransformer());
            transformerMap.put(Constants.NS_XSD + "date", new DateTransformer());
            transformerMap.put(Constants.NS_XSD + "time", new TimeTransformer());
            transformerMap.put(Constants.NS_XSD + "boolean", new BooleanTransformer());
            transformerMap.put(Constants.NS_XSD + "anyURI", new StringTransformer());
            transformerMap.put(Constants.NS_XSD + "string", new StringTransformer());

            xsdNodeTransformerMap = transformerMap;
        }


        private Map<String, SelectorFunction<Node>> xsdNodeFunctionMap;
        private void initFunctionMappings() {
            Map<String, SelectorFunction<Node>> functionMap = new HashMap<String, SelectorFunction<Node>>();

            registerFunction(functionMap, new ConcatenateFunction());
            registerFunction(functionMap, new FirstFunction());
            registerFunction(functionMap, new LastFunction());
            registerFunction(functionMap, new XPathFunction());
            registerFunction(functionMap, new RemoveXmlTagsFunction());
            registerFunction(functionMap, new CleanHtmlFunction());

            xsdNodeFunctionMap = functionMap;
        }

        private void registerFunction(Map<String, SelectorFunction<Node>> register, final SelectorFunction<Node> function) {
            register.put(Constants.NS_LMF_FUNCS + function.getPathExpression(backend), function);
        }

}
PARSER_END(RdfPathParser)

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
}

MORE:
{
 "\"" : WithinString
}

<WithinString> TOKEN:
{
  <STRLIT: "\""> : DEFAULT
}

<WithinString> MORE:
{
  <~["\n","\r"]>
}


TOKEN : /* OPERATORS */
{
	< AND:  "&"  >  |
	< OR:   "|"  >  |
	< IS:   "is" >  |
  	< TYPE: "^^" >
}

TOKEN :
{
    < URI: ["a"-"z","A"-"Z"](["a"-"z","A"-"Z","0"-"9","+","-","."])* "://"  (["a"-"z","A"-"Z","0"-"9",";","/","?",":","@","&","=","+","$",".","-","_","!","~","*","'","%"])+ ("#" (["a"-"z","A"-"Z","0"-"9",";","/","?",":","@","&","=","+","$",".","-","_","!","~","*","'","%"])*)? | "#" (["a"-"z","A"-"Z","0"-"9",";","/","?",":","@","&","=","+","$",".","-","_","!","~","*","'","%"])+> |
    < IDENTIFIER: ["a"-"z","A"-"Z","0"-"9","_"](["a"-"z","A"-"Z","0"-"9","_","'","-"])* > |
    < #URICHAR: ["a"-"z","A"-"Z","0"-"9",";","/","?",":","@","&","=","+","$",".","-","_","!","~","*","'","%"] >
}



Program Program() :
{
    Program<Node> program = new Program();
    NodeTest<Node> filter = null;
    FieldMapping<?,Node> rule;
    Token prefix = null;
    Token uri;
    NodeSelector<Node> boostSelector;
}
{
  (
    "@prefix" prefix = <IDENTIFIER> ":" "<" uri = <URI> ">" ";" {
        program.addNamespace(prefix.image, uri.image );
        namespaces.put(prefix.image,uri.image);
    }
  )*

  (
    "@filter" filter = NodeTest() ";" {
        program.setFilter(filter);
    }
  )?

  (
    "@boost" boostSelector = Selector() ";" {
    	NodeTransformer transformer = getTransformer(Program.DOCUMENT_BOOST_TYPE);
		FieldMapping booster = new FieldMapping("@boost", Program.DOCUMENT_BOOST_TYPE, boostSelector, transformer, null);
		program.setBooster(booster);  
    }
  )?

  (
    rule = Rule()
    {
       program.addMapping(rule);
    }
  )*
  <EOF>
  {
    return program;
  }
}

FieldMapping Rule() :
{
    FieldMapping<?,Node> rule;
    Token name;
    String type;
    NodeSelector<Node> selector;
    NodeTransformer<?,Node> transformer;
    Map<String, String> conf = null;
}
{
    name = <IDENTIFIER> "=" selector = Selector() "::" type = Uri() ("(" conf = FieldConfig() ")")? ";" {
        transformer = getTransformer(type);
        rule = new FieldMapping(name.image,type,selector,transformer, conf);
        return rule;
    }
}

Map<String,String> FieldConfig() : {
	Map<String, String> conf = new HashMap<String, String>();
	Token key = null;
	String val = null;
	Map<String,String> more = null;
}
{
	( key = <IDENTIFIER> "=" val = ConfVal() ("," more = FieldConfig() )? )? {
		if (key == null || val == null) return null;
		conf.put(key.image, val);
		if (more != null) {
			conf.putAll(more);
		}
		return conf;
	}
}

String ConfVal() : {
	Token str, id;
}
{
	str = <STRLIT> { return str.image.substring(1, str.image.length() -1); }
|	id = <IDENTIFIER> { return id.image; }
}

String Uri() : {
    Token uri, prefix, localName;

}
{
    "<" uri = <URI> ">" {
       return uri.image;
    }
|   prefix = <IDENTIFIER> ":" localName = <IDENTIFIER> {
        return resolveNamespace(prefix.image)+localName.image;
    }
}


NodeSelector Selector() :
{
    NodeSelector result;
}
{
    (
        result = CompoundSelector()
    |   result = TestingSelector()
    |   result = AtomicSelector()
    )
    {
        return result;
    }
}


NodeSelector CompoundSelector() :
{
    NodeSelector result = null;
}
{
    (
        /* Path Selector */
        result = PathSelector() |

        /* Intersection Selector */
        result = IntersectionSelector() |

        /* Union Selector */
        result = UnionSelector()
    )
    {
        return result;
    }
}


NodeSelector AtomicOrTestingSelector() :
{
    NodeSelector result = null;
}
{
    (
        /* Testing Selector */
        result = TestingSelector() |

        /* Atomic Selector */
        result = AtomicSelector()
    )
    {
        return result;
    }
}

NodeSelector AtomicSelector() :
{
    NodeSelector result = null;
}
{
    (
    	/* Self Selector */
    	result = SelfSelector() |
    	    
        /* Property Selector */
        result = PropertySelector() |

        /* Wildcard Selector */
        result = WildcardSelector() |
        
        /* Reverse Property Selector */
        result = ReversePropertySelector() |

        /* Function Selector */
        result = FunctionSelector() |

        /* String Constant Selector */
        result = StringConstantSelector() |
        
        /* Recursive Path Selector */
        result = RecursivePathSelector() |

        /* Other selector enclosed in braces */
        result = GroupedSelector()

    )
    {
        return result;
    }
}

NodeSelector SelfSelector() :
{
}
{
	"." { return new SelfSelector(); }
}

NodeSelector GroupedSelector() :
{
    NodeSelector result = null;
}
{
    /* Other selector enclosed in braces */
    "(" result = Selector() ")"
    {
        return result;
    }

}

RecursivePathSelector RecursivePathSelector() :
{
	RecursivePathSelector result = null;
	NodeSelector delegate        = null;
}
{
	"(" delegate = Selector() ")" "+"
	{
		result = new RecursivePathSelector(delegate);
		return result;
	}
}

PathSelector PathSelector() :
{
    PathSelector result = null;
    NodeSelector left   = null;
    NodeSelector right  = null;
}
{
    left = AtomicOrTestingSelector() "/" right = Selector()
    {
        result = new PathSelector(left,right);
        return result;
    }
}

IntersectionSelector IntersectionSelector() :
{
    IntersectionSelector result = null;
    NodeSelector left   = null;
    NodeSelector right  = null;
}
{
    left = AtomicOrTestingSelector() "&" right = Selector()
    {
        result = new IntersectionSelector(left,right);
        return result;
    }
}

UnionSelector UnionSelector() :
{
    UnionSelector result = null;
    NodeSelector left   = null;
    NodeSelector right  = null;
}
{
    left = AtomicOrTestingSelector() "|" right = Selector()
    {
        result = new UnionSelector(left,right);
        return result;
    }
}

TestingSelector TestingSelector() :
{
    TestingSelector result = null;
    NodeSelector delegate  = null;
    NodeTest test = null;
}
{
    delegate = AtomicSelector() "[" test = NodeTest() "]" {
        result = new TestingSelector(delegate,test);
        return result;
    }
}

ReversePropertySelector ReversePropertySelector() :
{
	ReversePropertySelector result = null;
	Node property;
    String uri;
}
{
	"^" uri = Uri() {
        property = resolveResource(uri);
        result   = new ReversePropertySelector(property);
        return result;
	}
}

PropertySelector PropertySelector() :
{
    PropertySelector result = null;
    Node property;
    String uri;
}
{
    uri = Uri() {
        property = resolveResource(uri);
        result   = new PropertySelector(property);
        return result;
    }
}

WildcardSelector WildcardSelector() :
{
    WildcardSelector result = null;
}
{
    "*" {
        result = new WildcardSelector();
        return result;
    }
}

FunctionSelector FunctionSelector() :
{
    FunctionSelector result = null;
    List<NodeSelector> arguments = new ArrayList<NodeSelector>();
    NodeSelector argument;
    String uri;
    Token fName;
}
{
    "fn:" fName = <IDENTIFIER> "("
            argument = Selector() { arguments.add(argument); }
            ( "," argument = Selector() { arguments.add(argument); } )*
    ")" {
           uri = namespaces.get("fn") + fName.image;
           result = new FunctionSelector(getFunction(uri),arguments);
           return result;
    }
}


StringConstantSelector StringConstantSelector() :
{
    StringConstantSelector result = null;
    Token literal;
}
{
    literal = <STRLIT> {
        result = new StringConstantSelector(literal.image.substring(1, literal.image.length()-1));
        return result;
    }
}



NodeTest NodeTest() :
{
    NodeTest result;
}
{
    (
        result = AndTest()
    |   result = OrTest()
    |   result = AtomicNodeTest()
    )
    {
        return result;
    }
}

NodeTest AtomicNodeTest() :
{
    NodeTest result;
}
{
    (
        result = LiteralLanguageTest()
    |   result = LiteralTypeTest()
    |   result = PathEqualityTest()
    |   result = PathTest()
    )
    {
        return result;
    }
}


LiteralLanguageTest LiteralLanguageTest():
{
    Token lang;
}
{
    "@" lang = <IDENTIFIER> {
        return new LiteralLanguageTest(lang.image);
    }
}

LiteralTypeTest LiteralTypeTest():
{
    String type;
}
{
    "^^" type = Uri() {
        return new LiteralTypeTest(type);
    }
}

AndTest AndTest():
{
    NodeTest left, right;
}
{
    left = AtomicNodeTest() "&" right = NodeTest() {
        return new AndTest(left,right);
    }
}

OrTest OrTest():
{
    NodeTest left, right;
}
{
    left = AtomicNodeTest() "|" right = NodeTest() {
        return new OrTest(left,right);
    }
}

PathEqualityTest PathEqualityTest():
{
    NodeSelector path;
    Node node;
}
{
    path = Selector() "is" node = Node() {
        return new PathEqualityTest(path,node);
    }
}


Node Node():
{
    String uri, type = null;
    Token literal, language;
}
{
    uri = Uri() {
        return resolveResource(uri);
    }
|   literal = <STRLIT>  ("^^" type = Uri() )? {
        try {
            return backend.createLiteral(literal.image.substring(1, literal.image.length()-1),null,new URI(type));
        } catch(java.net.URISyntaxException ex) {
            throw new ParseException("could not parse type URI "+type+": "+ex.getMessage());
	    }
    }
|   literal = <STRLIT>  "@" language = <IDENTIFIER> {
        return backend.createLiteral(literal.image.substring(1, literal.image.length()-1),new Locale(language.image),null);
    }
}


PathTest PathTest():
{
    NodeSelector path;
}
{
    (
        path = PathSelector()
    |   path = TestingSelector()
    |   path = AtomicSelector()
    )
    {
        return new PathTest(path);
    }
}

